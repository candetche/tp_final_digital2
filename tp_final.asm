	LIST	    P=16F887
	#INCLUDE    <P16F887.INC>
	
	; CONFIG1
	; __config 0x3FE4
	__CONFIG _CONFIG1, _FOSC_INTRC_NOCLKOUT & _WDTE_OFF & _PWRTE_ON & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_ON & _IESO_ON & _FCMEN_ON & _LVP_ON
	; CONFIG2
	; __config 0x3FFF
	__CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF
 
COUNT	EQU	0x70
AND1	EQU	0x71
AND2	EQU	0x72
BU_ST	EQU	0x73
BU_W	EQU	0x74
TECLADO	EQU	0x75 
CLEAR	EQU	0x76
SAVES	EQU	0x77
TEMP	EQU	0x78
DELTA	EQU	0x79
NUMERAL	EQU	0x7A
ASTERISCO EQU	0x7B
UNIDAD	EQU     0x7C
DECENA	EQU	0x7D
TIMER	EQU	0x7E
FLAG	EQU	0x7F
 
	
	ORG	0x4
	GOTO	INTER_SUB
	ORG	0x0
	GOTO    START
	ORG	0x5
	
	
; TODO -> DEBEMOS HACER QUE TOME SOLO LAS PULSACIONES EN FLANCO ASCENDENTE.
; TAMBIEN PODEMOS PONER UN  RETRASO AL ENTRAR A LAS INTRODUCCIONES

START
	; MOVEMOS AL BANCO 2
	BSF	STATUS,RP1
	BCF	STATUS,RP0
	

	; NUMEROS DEL TECLADO
	MOVLW	.0
	MOVWF	0x21
	
	MOVLW	.1
	MOVWF	0x48
	
	MOVLW	.2
	MOVWF	0x28
	
	MOVLW	.3
	MOVWF	0x18
	
	MOVLW	.4
	MOVWF	0x44
	
	MOVLW	.5
	MOVWF	0x24
	
	MOVLW	.6
	MOVWF	0x14
	
	MOVLW	.7
	MOVWF	0x42
	
	MOVLW	.8
	MOVWF	0x22
	
	MOVLW	.9
	MOVWF	0x12
	
	; VAMOS AL BANCO 0
	BCF	STATUS,RP1	
	
	MOVLW	B'10111011'
	MOVWF	NUMERAL	; AND PARA SABER SI ES NUMERAL

	MOVLW	B'10111110'
	MOVWF	ASTERISCO ; AND PARA SABER SI ES ASTERISCO
	
	MOVLW	B'01111000'
	MOVWF	AND1 ; PARA HACER EL ESCANEO
	
	MOVLW	B'00001010'
	MOVWF	AND2 ; AND DE 10
	
	MOVLW	B'00000111'
	MOVWF	PORTB  ; DEJAMOS LAS SALIDAS DEL PORTB EN 1 PARA Q ESTE LISTO PARA CUANDO SE APRIETE UNA TECLA DEL TECLADO
	CLRF	PORTC
	CLRF	PORTE

	MOVLW	.4
	MOVWF	COUNT ; CARGAMOS EL CONTADOR DE CICLOS DEL TIMER 1 (PARA EL BLINK DEL FINAL)
	
	MOVLW	0x20
	MOVWF	SAVES ; SAVES POINTER
	
	
	;---- TECLADO ----
	BANKSEL	TRISB
	MOVLW	B'11110000' ; <1:3> OUTPUTS, <4:7> INPUTS
	MOVWF	TRISB ; TECLADO
	
	; DISPLAYS, 7 SEGMENTOS Y LED COMO OUTPUT (TRISC Y TRISE)
	CLRF	TRISC ; OUTPUTS SEGMENTOS
	CLRF	TRISE ; OUTPUTS DISPLAYS Y LED
	
	; RA0 ES EL CANAL ANALOGICO PARA EL ADC
	CLRF	TRISA
	BSF	TRISA,0 ; SOLO EL BIT 0 DEL PORT A TIENE QUE SER ENTRADA
	
	;--- CONFIG TIMER 0 - SIN PRESCALER ---
	BCF	OPTION_REG, 5 ; T0CS - EL SOURCE TIENE QUE SER INTERNO
	BCF	OPTION_REG, 7 ; RBPU - RESISTENCIAS DE PULL-UP EN PORT B ACTIVADAS
	
	; TODO DIGITAL MENOS RA0 QUE ES EL CANAL QUE USAMOS EN EL ADC
	BANKSEL	ANSELH
	CLRF	ANSELH ; TODO PORTB DIGITAL
	MOVLW	0x01
	MOVWF	ANSEL ; AN0/RA0 CANAL ADC
	
	; ---- ADC ----
	BANKSEL ADCON0
	MOVLW	0x41 ; FOSC/8 PARA 4MHz Y ADON=1
	MOVWF	ADCON0
	
	BANKSEL ADCON1
	MOVLW	0x80 ; RIGHT JUSTIFIED Y VDD Y VSS COMO VOLT DE REFERENCIA
	MOVWF	ADCON1

	; --- CONFIG TIMER 1 ----
	BANKSEL T1CON
	MOVLW	0x00  ; NO HAY QUE PONERLO EN ON HASTA Q NO SE ESTE USANDO REALMENTE
	MOVWF	T1CON ; USA EL OSCILADOR INTERNO Y TIENE EL PS DESACTIVADO
	
	; ---- INTERRUPCIONES ----
	MOVLW	0x48  ; PEIE Y RBIE SET
	MOVWF	INTCON
	BCF	PIR1,0	
	
	BANKSEL	IOCB
	MOVLW	0x78 ; IOC PORTB <3:6> - INTERRUPCIONES POR CAMBIO DE ESTADO
	MOVWF	IOCB
	BSF	PIE1,TMR1IE ; INTERRUPCIONES POR TIMER 1
	BSF	INTCON,GIE ; INTERRUPCIONES GLOBALES
		
	BANKSEL	PORTB
	MOVLW	B'00001110'
	MOVWF	PORTB
	
L1	BTFSC	TECLADO,0 ; SI ESTA EN 1 ES PORQUE SE APRETO UNA TECLA
	CALL	SUB_TECLADO
	BTFSC	TIMER,0 ; SI ESTA EN 1 ES PORQUE INTERRUMPIO EL TIMER 1
	CALL	RETARDO_TECLAS
	GOTO	L1 ; TECLADO Y TIMER SOLO CAMBIAN EN LA RUTINA DE INTERRUPCION 
	

; HANDLER DE LAS INTERRUPCIONES GLOBALES	
INTER_SUB
	; ---- GUARDAMOS CONTEXTO ---- 
	MOVWF	BU_W
	SWAPF	STATUS,W
	MOVWF	BU_ST

	BANKSEL PORTB ; VAMOS AL BANCO 0
	
	; ---- VERIFICAMOS QUIEN INTERRUMPIO ---- 
	BTFSC	INTCON,RBIF ; FLAG DEL TECLADO
	COMF	TECLADO,F
	BTFSC	PIR1,TMR1IF ; FLAG DEL TIMER1
	CALL	RETARDO_TECLAS
	
	MOVF	PORTB,F
	BCF	INTCON,RBIF ; LIMPIAMOS LA FLAG DEL TECLADO
	BCF	PIR1,TMR1IF ; LIMPIAMOS LA FLAG DEL TIMER1

	
	; ---- RECUPERAMOS CONTEXTO ---- 
	SWAPF	BU_ST,W
	MOVWF	STATUS
	SWAPF	BU_W,F
	SWAPF	BU_W,W
	
	RETFIE

; SE LLAMA DESDE SUB DE INTERRUPCION	
RETARDO_TECLAS 
	BSF	INTCON,RBIE ; HABILITAMOS DE NUEVO LAS INTERRUPCIONES POR TECLADO
	BCF	T1CON,TMR1ON ; APAGAMOS EL TIMER1
	RETURN	
	
	
; ---- RUTINA DE ESCANEO DEL TECLADO ----  SE LLAMA DESDE EL MAIN
; NUMERAL RESETEA BUFFER Y HACE Q EL LED TITILE
; ASTERISCO SE USA PARA GUARDAR UN VALOR LEIDO EN EL ADC 
; CUALQUIER OTRO NUMERO MUESTRA EL VALOR QUE SE GUARDÓ PREVIAMENTE CON ASTERISCO EN EL BUFFER	
SUB_TECLADO	
	
	; RETARDO DEL TECLADO 0.5 SEG - PARA QUE TOME SOLO EL FLANCO ASCENDENTE
	CLRF	TMR1L
	CLRF	TMR1H 
	BSF	T1CON,0 ; PRENDEMOS EL TIMER
	
	BCF	INTCON,RBIE ; APAGAMOS LAS INTERRUPCIONES POR TECLADO
	
	COMF	TECLADO,F ; LIMPIAMOS LA FLAG
	BANKSEL PORTB
	CLRF	PORTB
	BSF	PORTB,0
	
ESCANEO	; LO RECORREMOS COMO MAXIMO 3 VECES
	MOVF	AND1,W
	ANDWF	PORTB,W 
	BTFSS	STATUS,Z 
	GOTO	ENCONTRADA ; Z = 0,ENCONTRAMOS LA TECLA
	; Z = 1, NO ENCONTRAMOS LA TECLA
	; DEBEMOS CAMBIAR AL SIGUIENTE OUTPUT 
	BCF	STATUS,C 
	RLF	PORTB,F
	GOTO	ESCANEO ; AL CAMBIAR EL OUTPUT VOLVEMOS A ESCANEAR
	
ENCONTRADA ; UNA VEZ QUE ENCONTRAMOS LA TECLA VENIMOS ACA
	
	; PRIMERO DEBEMOS VER SI ES NUMERAL O ASTERISCO
	MOVF	NUMERAL,W 
	ANDWF	PORTB,W
	BTFSC	STATUS,Z
	GOTO	RESETEAR ;  SI ES NUMERAL, LIMPIA EL BUFFER
	MOVF	ASTERISCO,W ; SI NO ES NUMERAL, REVISO SI ES ASTERISCO
	ANDWF	PORTB,W
	BTFSC	STATUS,Z ; SI DA 0, Z ES 1 ENTONCES ERA NUMERAL
	GOTO	GUARDAR ; ES NUMERAL, ASI QUE DEBEMOS GUARDAR LO Q HAY EN PORTB
	GOTO	MOSTRAR ; SI NO FUE NUM NI ASTERISCO ENTONCES APRETAMOS UN NUMERO Y TENEMOS QUE MOSTRAR EL VALOR GUARDADO
	

	
; --- LLEGA ACA DESDE SUB_TECLADO CUANDO SE APRETO UN NUMERAL ---	
GUARDAR	
	; VERIFICAMOS QUE GUARDAMOS SOLO 10 VALORES
	; SI YA GUARDAMOS NUEVE, VOLVEMOS A 0 - COMIENZA A SOBREESCRIBIR LOS VALORES VIEJOS 
	; BUFFER CIRCULAR
	
	INCF	DELTA,F
	MOVF	AND2,W
	XORWF	DELTA,W 
	BTFSC	STATUS,Z ; Z=0 CONTINUAMOS NORMALMENTE
	CLRF	DELTA ; Z=1 LIMPIAMOS DELTA
	
	; DEBEMOS MANDAR AL ADC A CONVERTIR, ESPERAR POR POLLING A QUE TERMINE
	; Y LUEGO TOMAR EL VALOR
	; PREVIAMENTE SE DEBE HABER MODIFICADO EL POTENCIOMETRO PARA Q HAYA UN CAMBIO
	
	BSF	ADCON0,1 ; LE DAMOS GO, COMIENZA A CONVERTIR
L2	BTFSS	PIR1,ADIF
	GOTO	L2 ; SI SIGUE EN 0 ES QUE NO TERMINO DE CONVERIR
	BCF	PIR1,ADIF ;TERMINO DE CONVERTIR, LIMPIAMOS LA FLAG
	
	BANKSEL	ADRESL ; ESTAMOS EN EL BANCO 1
	MOVF	ADRESL,W 
	MOVWF	TEMP
	BANKSEL PORTB ; VOLVEMOS AL BANCO 0
	; DEBEMOS GUARDAR LA LECTURA DEL ADC EN SAVES+DELTA
	MOVF	SAVES,W
	MOVWF	FSR ; PASAMOS EL PUNTERO DE DONDE VAMOS A GUARDAR 
	MOVF	DELTA,W
	ADDWF	FSR,F ; LE SUMAMOS EL DELTA
	MOVF	TEMP,W
	MOVWF	INDF ; CARGAMOS EL VALOR EN EL BUFFER CIRCULAR
	RETURN
	
	
; ---- RUTINA DONDE MOSTRAMOS LOS VALORES EN LOS DISPLAYS ---- 
; SE LLAMA DESDE SUB_TECLADO CUANDO SE APRIETA UN NUMERO (CADA NRO DEL TECLADO CORRESPONDE CON UNA DIRECCIÓN DE NUESTRO BUFFER CIRCULAR SAVES)	
MOSTRAR
	MOVF	PORTB,W		; EN W SE GUARDÓ LA CODIFICACION DE LA TECLA Q SE APRETO 
	BSF	STATUS,RP1
	BCF	STATUS,RP0	; EL VALOR QUE NECESITA ESTA EN EL BANCO 2
	MOVWF	FSR		; DIRECCION DONDE SE ENCUENTRA EL NUMERO AL QUE HACE REFERENCIA LA TECLA PRESIONADA
	MOVF	INDF,W		; TOMAMOS EL NUMERO QUE FUE PRESIONADO
	BCF	STATUS,RP1	; VOLVEMOS AL BANCO 0
	MOVWF	FSR		; PONEMOS EN FSR LO Q HABIA EN W QUE ERA EL NRO Q FUE PRESIONADO
	MOVF	SAVES,W		; PONEMOS EN W LA DIR Q TIENE SAVES (SAVES TIENE EL 0X20)
	ADDWF	FSR,F		; TECLA APRETADA + PUNTERO DONDE GUARDAMOS LAS LECTURAS DEL ADC
	
	MOVF	INDF,W ; OBTENEMOS LA LECTURA DEL ADC
	MOVWF	TEMP
	
	; SEPARACION DE NIBBLES
	MOVLW	0x0F
	ANDWF	TEMP,W ; UNIDAD - PRIMER NIBBLE
	MOVWF	UNIDAD
	
	MOVLW	0xF0
	ANDWF	TEMP,W ; DECENA - SEGUNDO NIBBLE
	MOVWF	DECENA
	SWAPF	DECENA,F ; SWAPEAMOS PARA QUE ESTE EN EL PRIMER NIBBLE

DISPLAY_LOOP
	
	; - CODIFICACION UNIDAD
	MOVF	UNIDAD,W
	CALL	CODIFICACION
	MOVWF	PORTC ; PASAMOS A LOS SEGMENTOS
	BSF	PORTE,0 ; PRENDEMOS LA UNIDAD
	CALL	T0_50HZ
	BCF	PORTE,0 ; APAGAMOS LA UNIDAD
	
	
	; - CODIFICACION DECENA
	MOVF	DECENA,W
	CALL	CODIFICACION
	MOVWF	PORTC ; PASAMOS A LOS SEGMENTOS
	BSF	PORTE,1 ; PRENDEMOS LA DECENA
	CALL	T0_50HZ
	BCF	PORTE,1 ; APAGAMOS LA DECENA
	
	; SI APRETAMOS OTRA TECLA MIENTRAS ESTABAMOS EN EL LOOP, DEBEMOS VOLVER 
	; AL MAIN.
	BTFSC	TECLADO,0 
	RETURN

	GOTO	DISPLAY_LOOP ; MOSTRAMOS HASTA QUE HAYA UNA INTERRUPCION DE NUEVO
	

; ---- CODIFICACION A 7 SEGMENTOS ----
CODIFICACION
	ADDWF	PCL,F
	RETLW	0x3F ; 0
	RETLW	0x06 ; 1
	RETLW	0x5B ; 2
	RETLW	0x4F ; 3
	RETLW	0x66 ; 4
	RETLW	0x6D ; 5
	RETLW	0x7D ; 6
	RETLW	0x07 ; 7
	RETLW	0x7F ; 8
	RETLW	0x67 ; 9
	RETLW	0x77 ; A
	RETLW	0x1F ; B
	RETLW	0x4E ; C
	RETLW	0x3D ; D
	RETLW	0x4F ; E
	RETLW	0x47 ; F
	
; --- SE LLAMA DESDE SUBRUTINA MOSTRAR ---	
T0_50HZ
	; 50HZ POR POLLING
	MOVLW	.178
	MOVWF	TMR0
	BTFSC	INTCON,T0IF
	GOTO	$-1
	BCF	INTCON,T0IF
	BCF	PORTE,1 ; APAGAMOS LA DECENA
	RETURN
	
; --- SE LLAMA CUANDO SE APRIETA NUMERAL ---	
RESETEAR
	MOVLW	.10
	MOVWF	CLEAR
	DECFSZ	CLEAR,F
	GOTO	BLINK ; SUBRUTINA DEL LED 
	MOVF	SAVES,W
	MOVWF	FSR
	CLRF	INDF
	GOTO	$-5
	
	
; ---- RUTINA DE PARPADEO DEL LED ---- SE LLAMA DESDE SUBRUTINA RESETEAR
BLINK	
	BTFSC	PORTE,2 ; CHEQUEA SI EL LED ESTA PRENDIDO O APAGADO
	GOTO	APAGAR
	BSF	PORTE,2 ; LED APAGADO, LO PRENDEMOS
	GOTO	SET_
	
APAGAR	BCF	PORTE,2 ; LED PRENDIDO, LO APAGAMOS
	DECFSZ	COUNT,F
	GOTO	SET_
	BCF	T1CON,0 ; SI HICMOS LOS 4 CICLOS, APAGAMOS EL TIMER
	MOVLW	.4
	MOVWF	COUNT ; VOLVEMOS A CARGAR EL CONTADOR
	RETURN	
	
SET_	; SETEAMOS EL CONTADOR CON 0x8000 O 32768
	MOVLW	0x00
	MOVWF	TMR1L
	MOVLW	0x80
	MOVWF	TMR1H 
	BSF	T1CON,0 ; PRENDEMOS EL TIMER
	
L3	BTFSS	PIR1,TMR1IF ; REVISAMOS POR POLLING EL TIMER 1
	GOTO	L3
	GOTO	BLINK
	
	
	END


